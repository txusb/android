apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'commons-net:commons-net:3.6'
    androidTestImplementation 'org.apache.commons:commons-lang3:3.8.1'
}

// the library is typically taken from jitpack.io with:
//   dependencies {
//     implementation 'com.github.kai-morich:usb-serial-for-android:1.x.0'
//   }
// for testing and local usage the library can be published to local maven repository
// with 'gradlew usbSerialForAndroid:publishToMavenLocal' using the following sections:
publishing {
    publications {
        maven(MavenPublication) {
            artifact bundleReleaseAar
            artifact androidSourcesJar
            groupId 'com.github.kai-morich'
            artifactId 'usb-serial-for-android'
            version '1.x.0'
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    from android.sourceSets.main.java.srcDirs
}

